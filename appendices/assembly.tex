%!TEX root = main.tex
% !TEX spellcheck = en-US
%%=========================================

\chapter{Context Switch Assembly}
\label{ch:assembly}

This appendix contains the assembly code used for context switching implementation.

\begin{lstlisting}[style={CustomAsm},caption={Context switch assembly for i386}]
; void context_switch(Context *from, Context *to);
   .globl context_switch          
   .type context_switch,@function 
context_switch:
    movl    0x04(%esp), %edx   ; from = %edx
; Store current snapshot in from-context
    movl    %ebx,   0x00(%edx) ; %ebx
    movl    %esi,   0x04(%edx) ; %esi
    movl    %edi,   0x08(%edx) ; %edi
    movl    %ebp,   0x0C(%edx) ; %ebp
    movl    %esp,   0x10(%edx) ; %esp

    movl    (%esp), %eax       ; %eip
    movl    %eax,   0x14(%edx) 

    movl    0x08(%esp), %edx   ; to = %edx
; Load to-context snapshot
    movl    0x00(%edx), %ebx   ; %ebx
    movl    0x04(%edx), %esi   ; %esi
    movl    0x08(%edx), %edi   ; %edi
    movl    0x0C(%edx), %ebp   ; %ebp
    movl    0x10(%edx), %esp   ; %esp

    movl    0x14(%edx), %eax   ; %eip
    movl    %eax,   (%esp)
    ret
.size context_switch, .-context_switch
\end{lstlisting}

\begin{lstlisting}[style={CustomAsm},caption={Context switch assembly for x86\_64}]
; void context_switch(Context *from, Context *to);
   .globl context_switch          
   .type context_switch,@function 
context_switch:
; from = %rdi, to = %rsi
; Store current snapshot in from-context
    movq    %rbx,   0x00(%rdi)  ; %rbx
    movq    %rsp,   0x08(%rdi)  ; %rsp
    movq    %rbp,   0x10(%rdi)  ; %rbp
    movq    %r12,   0x18(%rdi)  ; %r12
    movq    %r13,   0x20(%rdi)  ; %r13
    movq    %r14,   0x28(%rdi)  ; %r14
    movq    %r15,   0x30(%rdi)  ; %r15

    movq    (%rsp), %rax        ; %rip
    movq    %rax,   0x38(%rdi)
; Load to-context snapshot
    movq    0x00(%rsi), %rbx    ; %rbx
    movq    0x08(%rsi), %rsp    ; %rsp
    movq    0x10(%rsi), %rbp    ; %rbp
    movq    0x18(%rsi), %r12    ; %r12
    movq    0x20(%rsi), %r13    ; %r13
    movq    0x28(%rsi), %r14    ; %r14
    movq    0x30(%rsi), %r15    ; %r15

    movq    0x38(%rsi), %rax    ; %rip
    movq    %rax,   (%rsp)
    retq
.size context_switch, .-context_switch
\end{lstlisting}

